plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
    }
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'

    args '-r', 300
    args '-w', 300
    args '-t', 4
    args '-jvmArgsPrepend', '-Xmx8g'
    args '-jvmArgsPrepend', '-Xms8g'
}

shadowJar {
    from sourceSets.jmh.output
    from sourceSets.main.output
    from project.configurations.jmhRuntime

    manifest {
        attributes("Main-Class": "org.openjdk.jmh.Main")
    }
}

dependencies {
    jmhCompile project(':data-prepper-plugins:otel-trace-raw-prepper')
    jmhCompile "io.opentelemetry:opentelemetry-proto:${versionMap.opentelemetry_proto}"
    jmhCompile 'org.openjdk.jmh:jmh-core:1.25'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.25'
}